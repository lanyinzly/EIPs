---
eip: 7527
title: Token Bound Function Oracle AMM Contract
description: An interface system that wrap/unwrap between fungible tokens and non-fungible tokens based on an embedded Function Oracle AMM
author: Lanyin Zhang (@lanyinzly) <lz8aj@virginia.edu>, Jerry <jerrymindflow@gmail.com>, Amandafanny <amandafanny200@gmail.com>, Shouhao Wong (@wangshouh) <wongshouhao@outlook.com>
discussions-to: https://ethereum-magicians.org/t/eip-7527-token-bound-function-oracle-amm-contract/15950
status: Draft
type: Standards Track
category: ERC
created: 2023-09-03
---


## Abstract

This proposal outlines a system that incorporates a Function Oracle capable of converting fungible tokens into a single non-fungible token each instance and then reverting the non-fungible token back to fungible tokens. The pre-configured Function Oracle comes equipped with its own Automated Market Maker (AMM), facilitating a clearing mechanism for voucher issuers.

## Motivation

The motivation behind a wrapping system with embedded Function Oracle AMM is to achieve decentralized credit endorsement and thus quantifiable decentralized credit assets with liquidity in nature. This creates an applicable infrastructure for credit creation, transmission, and oracle in payment, social, and financial context. Decentralized credit voucher can be implemented in the form of Non-fungible tokens, compatible with current Ethereum environment, which also expands its application scenarios. The concept of decentralized credit is highly integrated with dApps development. It aims to foster a more trustless environment.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

Contract Interfaces: 

Three interfaces are included here: Agency, App, and Factory. 

Agency and App MAY be seperately implemented, but they SHALL be mutually bounded after initialization. Or, Agency and App MAY be both implemented in one single contract. 

Factory Interface is OPTIONAL, depending on whether the combination of Agency and App need to be deployed repeated. 

The Agancy MUST implement an Function as Oracle, which serves as AMM when token are wrapped or unwrapped. The App serves as a single entry point for all wrap and unwrap queries. 

### Agency Interface

```

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

/**
 * @dev The settings of the agency.
 * @param currency The address of the currency. If `currency` is 0, the currency is Ether.
 * @param premium The base premium of the currency.
 * @param feeRecipient The address of the fee recipient.
 * @param mintFeePercent The fee of minting.
 * @param burnFeePercent The fee of burning.
 */
struct Asset {
    address currency;
    uint256 basePremium;
    address feeRecipient;
    uint16 mintFeePercent;
    uint16 burnFeePercent;
}
interface IERC7527Agency{
    /**
     * @dev Allows the account to receive Ether
     *
     * Accounts MUST implement a `receive` function.
     *
     * Accounts MAY perform arbitrary logic to restrict conditions
     * under which Ether can be received.
     */
    receive() external payable;

    /**
     * @dev Emitted when `tokenId` token is wrapped.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param tokenId The identifier of the newly created non-fungible token.
     * @param price The price of wrapping.
     * @param fee The fee of wrapping.
     */
    event Wrap(
        address indexed to,
        uint256 indexed tokenId,
        uint256 price,
        uint256 fee
    );

    /**
     * @dev Emitted when `tokenId` token is unwrapped.
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param price The price of unwrapping.
     * @param fee The fee of unwrapping.
     */
    event Unwrap(
        address indexed to,
        uint256 indexed tokenId,
        uint256 price,
        uint256 fee
    );

    /**
     * @dev Wrap basePremium into a non-fungible token.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into ifself and the newly created non-fungible token.
     * @return The identifier of the newly created non-fungible token.
     */
    function wrap(address to, bytes calldata data) external payable returns (uint256);

    /**
     * @dev Unwrap a non-fungible token into currency basePremium.
     * 
     * Todo: event
     *
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param data The data to encode into ifself and the non-fungible token with identifier `tokenId`.
     */
    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable;

    /**
     * @dev Returns the strategy of the agency.
     * @return app The address of the app.
     * @return asset The asset of the agency.
     * @return attributeData The attributeData of the agency.
     */
    function getStrategy() external view returns(address app, Asset memory asset, bytes memory attributeData);

    /**
     * @dev Returns the price and fee of unwrapping.
     * @param data The data to encode to calculate the price and fee of unwrapping.
     * @return price The price of wrapping.
     * @return fee The fee of wrapping.
     */
    function getUnwrapOracle(bytes memory data) external view returns(uint256 price, uint256 fee);

    /**
     * @dev Returns the price and fee of wrapping.
     * @param data The data to encode to calculate the price and fee of wrapping.
     * @return price The price of wrapping.
     * @return fee The fee of wrapping.
     */
    function getWrapOracle(bytes memory data) external view returns(uint256 price, uint256 fee);
 }

 ```

### App Interface

``` 

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

interface IERC7527App {
    /**
     * @dev Returns the maximum supply of the non-fungible token.
     */
    function getMaxSupply() external view returns (uint256);

    /**
     * @dev Returns the name of the non-fungible token with identifier `id`.
     * @param id The identifier of the non-fungible token.
     */
    function getName(uint256 id) external view returns (string memory);

    /**
     * @dev Returns the agency of the non-fungible token.
     */
    function getAgency() external view returns (address payable);

    /**
     * @dev Sets the agency of the non-fungible token.
     * @param agency The agency of the non-fungible token.
     */
    function setAgency(address payable agency) external;

    /**
     * @dev Mints a non-fungible token to `to`.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into the newly created non-fungible token.
     */
    function mint(address to, bytes calldata data) external returns (uint256);

    /**
     * @dev Burns a non-fungible token with identifier `tokenId`.
     * @param tokenId The identifier of the non-fungible token to burn.
     * @param data The data to encode into the non-fungible token with identifier `tokenId`.
     */
    function burn(uint256 tokenId, bytes calldata data) external;
}

```

### Factory Interface 

If a factory is needed to deploy bounded App and Agency, the factory SHALL implement the following interface:

```

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Asset} from "./IERC7527Agency.sol";

/**
 * @dev The settings of the agency.
 * @param implementation The address of the agency implementation.
 * @param asset The parameter of asset of the agency.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of agencyImplementation.
 * @param initData If init data is not empty, calls proxy contract of agencyImplementation with this data.
 */
struct AgencySettings {
    address payable implementation;
    Asset asset;
    bytes immutableData;
    bytes initData;
}

/**
 * @dev The settings of the app.
 * @param implementation The address of the app implementation.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of appImplementation.
 * @param initData If init data is not empty, calls proxy contract of appImplementation with this data.
 */
struct AppSettings {
    address implementation;
    bytes immutableData;
    bytes initData;
}

interface IERC7527Factory {
    /**
     * @dev Deploys a new agency and app clone and initializes both.
     * @param agencySettings The settings of the agency.
     * @param appSettings The settings of the app.
     * @param data The data is additional data, it has no specified format and it is sent in call to `factory`.
     * @return appInstance The address of the created proxy contract of appImplementation.
     * @return agencyInstance The address of the created proxy contract of agencyImplementation.
     */
    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata data)
        external
        returns (address, address);
}

```

## Rationale

### Mutual Bound

Implement contracts for `IERC7527App` and `IERC7527Agency` so that they are each other's only owner. The wrap process is to check the premium amount of the fungible token received and then mint non-fungible token in the App. Only the holder of the non-fungible token can unwrap it.

### Implementation Diversity : 

Users and customize function and fee percentage when implement the Agency and the App interfaces.

Different Agency implementations have distinct wrap, unwrap function logic, and different oracleFunction. Users can customize the currency, initial price, fee receiving address, fee rate, etc., to initialize the Agency proxy contract. 

Different App implementations cater to various use cases. They could be movie tickets, deposit receipts, bonds, stocks, and so on. Users can initialize the App proxy contract.

Factory is not required. Factory implementation is need-based. Users can deploy their own proxy contracts by selecting different Agency implementations and different App implementations through the Factory, combining them to create various products.


### Currency types and functions

Support ETH and [ERC-20](./eip-20.md).

### [ERC-721](./eip-721.md) - Required for ID

For each wrap process, a unique ID should be generated. This ID is essential for verification during the unwrap process. It also serves as the exclusive credential for the token. This mechanism ensures the security of assets within the Agency. While it's suggested, it's not mandatory to use the [ERC-721](./eip-721.md) standard.

### Wrap and Mint

The strategy is set while implementing the Agency interface, which ensures it's not upgradable.

When executing the 'wrap' function, the predetermined strategy parameters are passed into the 'getWrapOracle' function to fetch the current price and fees. The respective premium is then transferred to the Agency instance and the fee receiving address. Subsequently, App tokens are minted to the user's address.

Need to check whether mint and wrap are mutually bounded and check uniqueness: Tokens transferred into the Agency cannot be moved, except through the unwrap process. The act of executing wrap is the sole trigger for the mint process. 

### Unwrap and Burn

"When executing the 'unwrap' function, predetermined strategy parameters are passed into the 'getUnwrapOracle' function to read the current price and fees. The corresponding premium is then transferred to the user's address and the fee receiving address, after which the App token is burned. The act of executing 'unwrap' is the sole trigger for the 'burn' process.

### Two interfaces use together

Two interfaces and be implemented together for safety, but they can be independently implemented before initialization for flexibiliy.

### Pricing

getWrapOracle and getUnwrapOracle are used to fetch the current price and fees. They implement on-chain price fetching through oracle functions. They not only support fetching the price and fees during the wrap and unwrap processes but also support other contracts calling them to obtain the price and fees, such as lending contracts.

Support Oracles based on on-chain and off-chain parameters. Suggest on-chain parameters only for consensus of on-chain reality.


## Backwards Compatibility

No backward compatibility issues found.


## Reference Implementation

```

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {
    ERC721Enumerable,
    ERC721,
    IERC721Enumerable
} from "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {ClonesWithImmutableArgs} from "clones-with-immutable-args/ClonesWithImmutableArgs.sol";
import {IERC7527App} from "./interfaces/IERC7527App.sol";
import {IERC7527Agency, Asset} from "./interfaces/IERC7527Agency.sol";
import {IERC7527Factory, AgencySettings, AppSettings} from "./interfaces/IERC7527Factory.sol";

contract ERC7527Agency is IERC7527Agency {
    using Address for address payable;

    modifier onlyApp() {
        uint256 offset = _getImmutableArgsOffset();
        address app;
        assembly {
            app := shr(0x60, calldataload(add(offset, 76)))
        }
        require(msg.sender == app, "ERC7527Agency: caller is not the app");
        _;
    }

    receive() external payable {}

    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable override {
        (address _app, Asset memory _asset,) = getStrategy();
        require(_isApprovedOrOwner(_app, msg.sender, tokenId), "LnModule: not owner");
        IERC7527App(_app).burn(tokenId, data);
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 swap, uint256 burnFee) = getUnwrapOracle(abi.encode(_sold));
        _transfer(address(0), payable(to), swap - burnFee);
        _transfer(address(0), _asset.feeRecipient, burnFee);
        emit Unwrap(to, tokenId, swap, burnFee);
    }

    function wrap(address to, bytes calldata data) external payable override returns (uint256) {
        (address _app, Asset memory _asset,) = getStrategy();
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 swap, uint256 mintFee) = getWrapOracle(abi.encode(_sold));
        require(msg.value >= swap + mintFee, "ERC7527Agency: insufficient funds");
        _transfer(address(0), _asset.feeRecipient, mintFee);
        if (msg.value > swap + mintFee) {
            _transfer(address(0), payable(msg.sender), msg.value - swap - mintFee);
        }
        uint256 id_ = IERC7527App(_app).mint(to, data);
        require(_sold + 1 == IERC721Enumerable(_app).totalSupply(), "ERC7527Agency: Reentrancy");
        emit Wrap(to, id_, swap, mintFee);
        return id_;
    }

    function getStrategy() public pure override returns (address app, Asset memory asset, bytes memory attributeData) {
        uint256 offset = _getImmutableArgsOffset();
        address currency;
        uint256 premium;
        address payable awardFeeRecipient;
        uint16 mintFeePercent;
        uint16 burnFeePercent;
        assembly {
            app := shr(0x60, calldataload(add(offset, 0)))
            currency := shr(0x60, calldataload(add(offset, 20)))
            premium := calldataload(add(offset, 40))
            awardFeeRecipient := shr(0x60, calldataload(add(offset, 72)))
            mintFeePercent := shr(0xf0, calldataload(add(offset, 92)))
            burnFeePercent := shr(0xf0, calldataload(add(offset, 94)))
        }
        asset = Asset(currency, premium, awardFeeRecipient, mintFeePercent, burnFeePercent);
        attributeData = "";
    }

    function getUnwrapOracle(bytes memory data) public pure override returns (uint256 swap, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        swap = _asset.premium + input * _asset.premium / 100;
        fee = swap * _asset.burnFeePercent / 10000;
    }

    function getWrapOracle(bytes memory data) public pure override returns (uint256 swap, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        swap = _asset.premium + input * _asset.premium / 100;
        fee = swap * _asset.mintFeePercent / 10000;
    }

    function _transfer(address currency, address recipient, uint256 premium) internal {
        if (currency == address(0)) {
            payable(recipient).sendValue(premium);
        } else {
            IERC20(currency).transfer(recipient, premium);
        }
    }

    function _isApprovedOrOwner(address app, address spender, uint256 tokenId) internal view virtual returns (bool) {
        IERC721Enumerable _app = IERC721Enumerable(app);
        address _owner = _app.ownerOf(tokenId);
        return (spender == _owner || _app.isApprovedForAll(_owner, spender) || _app.getApproved(tokenId) == spender);
    }
    /// @return offset The offset of the packed immutable args in calldata

    function _getImmutableArgsOffset() internal pure returns (uint256 offset) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            offset := sub(calldatasize(), add(shr(240, calldataload(sub(calldatasize(), 2))), 2))
        }
    }
}

contract ERC7527App is ERC721Enumerable, IERC7527App {
    constructor() ERC721("ERC7527App", "EA") {}

    address payable private _oracle;

    modifier onlyAgency() {
        require(msg.sender == _getAgency(), "only agency");
        _;
    }

    function getName(uint256) external pure returns (string memory) {
        return "App";
    }

    function getMaxSupply() public pure override returns (uint256) {
        return 100;
    }

    function getAgency() external view override returns (address payable) {
        return _getAgency();
    }

    function setAgency(address payable oracle) external override {
        require(_getAgency() == address(0), "already set");
        _oracle = oracle;
    }

    function mint(address to, bytes calldata data) external override onlyAgency returns (uint256 tokenId) {
        require(totalSupply() < getMaxSupply(), "max supply reached");
        tokenId = abi.decode(data, (uint256));
        _mint(to, tokenId);
    }

    function burn(uint256 tokenId, bytes calldata) external override onlyAgency {
        _burn(tokenId);
    }

    function _getAgency() internal view returns (address payable) {
        return _oracle;
    }
}

contract ERC7527Factory is IERC7527Factory {
    using ClonesWithImmutableArgs for address;

    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata)
        external
        override
        returns (address appInstance, address agencyInstance)
    {
        appInstance = appSettings.implementation.clone(appSettings.immutableData);
        {
            agencyInstance = address(agencySettings.implementation).clone(
                abi.encodePacked(
                    appInstance,
                    agencySettings.asset.currency,
                    agencySettings.asset.premium,
                    agencySettings.asset.feeRecipient,
                    agencySettings.asset.mintFeePercent,
                    agencySettings.asset.burnFeePercent,
                    agencySettings.immutableData
                )
            );
        }

        IERC7527App(appInstance).setAgency(payable(agencyInstance));
        if (agencySettings.initData.length != 0) {
            (bool success, bytes memory result) = agencyInstance.call(agencySettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }

        if (appSettings.initData.length != 0) {
            (bool success, bytes memory result) = appInstance.call(appSettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }
    }
}

```

## Security Considerations

### Fraud Prevention

Consider the following for the saftey of the contracts system:

* Check whether `ERC7527Agency` and `ERC7527App` are mutually bounded

* Check the function strategies

* Check whether the system can be subject to reentrancy attack

* Check whether all non-fungible tokens can be unwrapped with the premium calculated from Function Oracle AMM.
  
* Check whether no fungible tokens would be left in the contract account when all non-fungible Tokens are unwrapped. 

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
